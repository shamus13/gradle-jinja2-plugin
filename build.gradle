plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'

    alias(libs.plugins.gradle.publish)
    alias(libs.plugins.nebula.release)
    alias(libs.plugins.nebula.lint)
    alias(libs.plugins.changelog)
}

repositories {
    mavenCentral()
}

gradleLint {
    rules = ['all-dependency']
}

pluginBundle {
    website = 'https://github.com/shamus13/gradle-jinja2-plugin'
    vcsUrl = 'https://github.com/shamus13/gradle-jinja2-plugin'

    description = 'Gradle plugin for generating files based on Jinja2 templates. The plugin uses jinjava as template engine.'

    pluginTags = [
            jinja2: ['jinja2'],
    ]
}

gradlePlugin {
    plugins {
        jinja2 {
            id = 'dk.grixie.jinja2.gradle-jinja2-plugin'
            implementationClass = 'dk.grixie.jinja2.gradle.plugin.Jinja2Plugin'
            displayName = 'Jinja2 plugin'
        }
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation(libs.jinja2) {
        exclude group: 'com.google.guava', module: 'guava'
    }

    implementation(libs.guava)

    testImplementation(libs.junit)
    testImplementation(libs.bundles.mockito)
    testImplementation(libs.assertj)
}

tasks.publish.dependsOn(publishPlugins)